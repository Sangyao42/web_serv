;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Date   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

obs-date             = rfc850-date / asctime-date

rfc850-date          = day-name-l "," SP date2 SP time-of-day SP GMT
asctime-date         = day-name SP date3 SP time-of-day SP year

date2                = day "-" month "-" 2DIGIT
date3                = month SP ( 2DIGIT / ( SP DIGIT ) )

day-name-l =
 / %x4D.6F.6E.64.61.79 ; Monday
 / %x54.75.65.73.64.61.79 ; Tuesday
 / %x57.65.64.6E.65.73.64.61.79 ; Wednesday
 / %x54.68.75.72.73.64.61.79 ; Thursday
 / %x46.72.69.64.61.79 ; Friday
 / %x53.61.74.75.72.64.61.79 ; Saturday
 / %x53.75.6E.64.61.79 ; Sunday

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Field   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; RFC9110 6.6.2
Trailer             = [ field-name *( OWS "," OWS field-name ) ]

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Message Forwarding (RFC9110 7.6)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Connection          = [ connection-option *( OWS "," OWS connection-option ) ]
connection-option   = token

//// for keep-alive and close tokens?
//// but keep-alive is not necessary in HTTP1.1
//// and don't know it we have any use case of close

Max-Forwards        = 1*DIGIT

Via                 = [ ( received-protocol RWS received-by [ RWS comment ] )
 *( OWS "," OWS ( received-protocol RWS received-by [ RWS comment ] ) ) ]
received-by         = pseudonym [ ":" port ]
received-protocol   = [ protocol-name "/" ] protocol-version
pseudonym           = token

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Upgrade (RFC 9110 7.8)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Upgrade             = [ protocol *( OWS "," OWS protocol ) ]
protocol            = protocol-name [ "/" protocol-version ]
protocol-name       = token
protocol-version    = token

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Validator fields (RFC9110 8.8)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Last-Modified       = HTTP-date

ETag                = entity-tag
entity-tag          = [ weak ] opaque-tag
etagc               =
 "!"
 / %x23-7E ; '#'-'~'
 / obs-text
opaque-tag          = DQUOTE *etagc DQUOTE
weak                = %x57.2F ; W/

//// when we use If-Modified-Since / If-None-Match header header in GET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Representation Data and Metadata (RFC9110 8)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Content-Encoding    = [ content-coding *( OWS "," OWS content-coding ) ]
content-coding      = token

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Request Context fields (RFC9110 10.1)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Expect              = [ expectation *( OWS "," OWS expectation ) ]
expectation         = token [ "=" ( token / quoted-string ) parameters ]
//// Expect for a 100-continue expectation

From                = mailbox
mailbox             = <mailbox, see [RFC5322], Section 3.4>

TE                  = [ t-codings *( OWS "," OWS t-codings ) ]
t-codings           = "trailers" / ( transfer-coding [ weight ] )
transfer-coding     = token *( OWS ";" OWS transfer-parameter )
transfer-parameter  = token BWS "=" BWS ( token / quoted-string )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   HTTP Authentication (RFC9110 11)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Proxy-Authenticate        = [ challenge *( OWS "," OWS challenge ) ]
Proxy-Authorization       = credentials
Proxy-Authentication-Info = [ auth-param *( OWS "," OWS auth-param ) ]

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Content Negotiation (RFC9110 12.5)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Accept-Encoding     = [ ( codings [ weight ] )         *( OWS "," OWS ( codings [ weight ] ) ) ]
codings             = content-coding / "identity" / "*"

Vary                = [ ( "*" / field-name )           *( OWS "," OWS ( "*" / field-name ) ) ]

//// no idea about Vary

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Conditional Request (RFC9110 13)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

If-Match            = "*" / [ entity-tag *( OWS "," OWS entity-tag ) ]
If-None-Match       = "*" / [ entity-tag *( OWS "," OWS entity-tag ) ]
If-Modified-Since   = HTTP-date
If-Unmodified-Since = HTTP-date
If-Range            = entity-tag / HTTP-date

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;   Range request (RFC9110 14)   ;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Range header field (RFC9110 14.2)
Range               = ranges-specifier

; Accept-Ranges header field (RFC9110 14.3)
Accept-Ranges       = acceptable-ranges
acceptable-ranges   = range-unit *( OWS "," OWS range-unit )

; Content-Range header field (RFC9110 14.4)
Content-Range       = range-unit SP ( range-resp / unsatisfied-range )
range-resp          = incl-range "/" ( complete-length / "*" )
incl-range          = first-pos "-" last-pos
unsatisfied-range   = "*/" complete-length
complete-length     = 1*DIGIT

; Range units (RFC9110 14.1)
range-unit          = token

ranges-specifier    = range-unit "=" range-set
range-set           = range-spec *( OWS "," OWS range-spec )
range-spec          = int-range / suffix-range / other-range

first-pos           = 1*DIGIT
last-pos            = 1*DIGIT
int-range           = first-pos "-" [ last-pos ]
suffix-length       = 1*DIGIT
suffix-range        = "-" suffix-length
other-range         = 1*(
   %x21-2B ; '!'-'+'
 / %x2D-7E ; '-'-'~'
)

//// could be optional